group 'com.kpearce'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    ajc
    aspects
    aspectCompile
    compile {
        extendsFrom aspects
    }
}

dependencies {
    ext.springVersion = '4.2.7.RELEASE'
    ext.aspectjVersion = '1.8.5'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'

    compile "org.springframework:spring-context:$springVersion"
    compile "org.springframework:spring-test:$springVersion"
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // aspect compilation
    ajc "org.aspectj:aspectjtools:$aspectjVersion"
    compile "org.aspectj:aspectjrt:$aspectjVersion"
    aspects "org.springframework:spring-aspects:$springVersion"
}

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
//    //The following two lines are useful if you have queryDSL if not ignore
//    dependsOn generateQueryDSL
//    source generateQueryDSL.destinationDir

    dependsOn configurations.ajc.getTaskDependencyFromProjectDependency(true, "compileJava")

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath)
        ant.iajc(source: "1.8", target: "1.8", destDir: sourceSets.main.output.classesDir.absolutePath, maxmem: "512m", fork: "true",
                aspectPath: configurations.aspects.asPath,
                sourceRootCopyFilter: "**/.svn/*,**/*.java", classpath: configurations.compile.asPath) {
            sourceroots {
                sourceSets.main.java.srcDirs.each {
                    pathelement(location: it.absolutePath)
                }
            }
        }
    }
}